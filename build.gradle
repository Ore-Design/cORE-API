plugins {
	id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'tech.yanand.maven-central-publish' version '1.3.0'
}

java {
	withSourcesJar()
	withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
			from components.java
			pom {
		        name = 'cORE-API'
		        description = "API for integrations to Ore Design's cORE software server"
                url = 'http://c.ore.design'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html#license-text'
                    }
                }
                developers {
                    developer {
                        id = 'jbatista'
                        name = 'Jacob Batista'
                        email = 'jbatista@ore.design'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Ore-Design/cORE-API.git'
                    developerConnection = 'scm:git:ssh://github.com/Ore-Design/cORE-API.git'
                    url = 'http://c.ore.design'
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(findProperty('signingKey'), findProperty('signingPassword'))
    sign publishing.publications.mavenJava
}

mavenCentral {
    authToken = (findProperty('sonatypeUsername') + ':' + findProperty('sonatypePassword')).bytes.encodeBase64().toString()
}

sourceCompatibility = 21
targetCompatibility = 21

def packagename = "core"
archivesBaseName = "${packagename}"
group = "design.ore.api"
version = "2.0.14"

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
	options.generatedSourceOutputDirectory = file("src/generated/java")
}

sourceSets {
	main {
		java.srcDirs += 'src/generated/java'
	}
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.32"
    annotationProcessor "org.hibernate.orm:hibernate-jpamodelgen:6.5.2.Final"
    
    implementation "org.projectlombok:lombok:1.18.32"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.17.0"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.17.0"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.0"
    implementation "org.hibernate.orm:hibernate-jpamodelgen:6.5.2.Final"
}

sourcesJar.dependsOn compileJava
